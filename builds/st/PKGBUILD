# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=master
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
_patches=(http://st.suckless.org/patches/st-scrollback-20170329-149c0d3.diff
          http://st.suckless.org/patches/st-scrollback-mouse-20170427-5a10aca.diff
	  http://st.suckless.org/patches/st-scrollback-mouse-altscreen-20170427-5a10aca.diff)
source=(st-master.tar.gz
        config.h
	"${_patches[@]}")
md5sums=('0ab55597a159aea89cdedffa1616d813'
         'bba84819c0b0bc93788a9ae9476fff98'
         'd858964e5123f6279a6f45ad57131ad7'
         '289cecdaa5421208de623958ee727614'
         '7c45639ae262c77b8aadd2b8d44d92fe')


prepare() {
  cd "$srcdir"/$pkgname-$pkgver
  # skip terminfo which conflicts with nsurses
  sed -i '/\@tic /d' Makefile
  cp "$srcdir"/config.h config.h
  for patch in "${_patches[@]}"; do
    echo "applying patch $(basename $patch)..."
    patch -Np1 -i "$srcdir/$(basename $patch)"
  done
}

build() {
  cd "$srcdir"/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$srcdir"/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
